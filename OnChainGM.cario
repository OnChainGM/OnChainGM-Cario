use starknet::ContractAddress;

#[starknet::interface]
trait IOnchainGM<TContractState> {
    fn onChainGM(ref self: TContractState, value: u256);
    fn timeUntilNextGM(self: @TContractState, user: ContractAddress) -> u256;
    fn get_fee_recipient(self: @TContractState) -> ContractAddress;
    fn get_gm_fee(self: @TContractState) -> u256;
    fn get_last_GM_timestamp(self: @TContractState, address: ContractAddress) -> u256;
}

#[starknet::contract]
mod onchaingm {
    use openzeppelin_token::erc20::interface::{ERC20ABIDispatcher, ERC20ABIDispatcherTrait};
    use starknet::event::EventEmitter;
    use starknet::storage::{
        Map, StorageMapReadAccess, StorageMapWriteAccess, StoragePointerReadAccess,
        StoragePointerWriteAccess,
    };
    use starknet::{
        ContractAddress, contract_address_const, get_block_timestamp, get_caller_address,
    };

    const TIME_LIMIT: u256 = 86400;

    #[storage]
    struct Storage {
        fee_recipient: ContractAddress,
        GM_FEE: u256,
        last_GM_timestamp: Map<ContractAddress, u256>,
        FEE_TOKEN: ContractAddress,
    }

    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
        OnChainGMEvent: OnChainGMEvent,
    }

    #[derive(Drop, starknet::Event)]
    struct OnChainGMEvent {
        sender: ContractAddress,
    }

    #[constructor]
    fn constructor(ref self: ContractState) {
        self
            .FEE_TOKEN
            .write(
                contract_address_const::<
                    0x04718f5a0fc34cc1af16a1cdee98ffb20c31f5cd61d6ab07201858f4287c938d,
                >(),
            );
        self.fee_recipient.write(contract_address_const::<
            0x077e6508cdd61b683fd3004bb2c2096229256f5e31e0d28ba0fb1a3748089a66,
        >());
        self.GM_FEE.write(500000000000000000);
    }

    #[external(v0)]
    impl onchaingm of super::IOnchainGM<ContractState> {
        fn onChainGM(ref self: ContractState, value: u256) {
            let GM_FEE: u256 = self.GM_FEE.read();
            let caller: ContractAddress = get_caller_address();
            let fee_recipient: ContractAddress = self.fee_recipient.read();

            assert!(value == GM_FEE, "msg.value not equal to GM_FEE");

            let last_gm = self.get_last_GM_timestamp(caller);
            let current_time: u256 = get_block_timestamp().into();

            assert!(
                current_time >= last_gm + TIME_LIMIT || last_gm == 0,
                "wait 24 hours",
            );

            self.last_GM_timestamp.write(caller, current_time);

            // Transfer from caller -> fee_recipient (caller must approve first)
            let success: bool = ERC20ABIDispatcher { contract_address: self.FEE_TOKEN.read() }
                .transfer_from(caller, fee_recipient, value);

            assert!(success == true, "Fee transfer failed");

            self.emit(OnChainGMEvent { sender: caller });
        }

        fn get_last_GM_timestamp(self: @ContractState, address: ContractAddress) -> u256 {
            self.last_GM_timestamp.read(address)
        }

        fn timeUntilNextGM(self: @ContractState, user: ContractAddress) -> u256 {
            let last_gm = self.get_last_GM_timestamp(user);
            let current_time: u256 = get_block_timestamp().into();

            if last_gm == 0 || current_time <= last_gm {
                return 0;
            }

            let time_passed: u256 = current_time - last_gm;

            if time_passed >= TIME_LIMIT {
                return 0;
            }

            return TIME_LIMIT - time_passed;
        }

        fn get_fee_recipient(self: @ContractState) -> ContractAddress {
            self.fee_recipient.read()
        }

        fn get_gm_fee(self: @ContractState) -> u256 {
            self.GM_FEE.read()
        }
    }
}
